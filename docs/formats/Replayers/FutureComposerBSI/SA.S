START:	MOVE.L 4,A6
	JSR -132(A6)
	LEA COP2(PC),A0
	LEA COPA(PC),A1
	MOVE.L A0,(A1)
	LEA COP3(PC),A0
	MOVE.L A0,4(A1)
	BRA ST2

COPA:	DC.L 0
	DC.L 0
CURCOP:	DC.W 0

COPLIST:DC.L $0001FFFE,$01000000
	DC.L $2B01FFFE,$00960020,$01440000,$01460000,$009A0070
	DC.L $008E0581,$01000000,$01040024,$0090F1C1
	DC.L $00920038,$009400D0,$01020000,$01080000,$010A0000
	DC.L $01800000,$01820FFF,$01840C40,$018600EF
	DC.L $01880F5F,$018A00C6,$018C000D,$018E0FE8
	DC.L $01900C70,$01920950,$01940F86,$01960666
	DC.L $01980999,$019A00FB,$019C009E,$019E0CCC
PLANES:	DC.L $00E00000,$00E20000,$00E40000,$00E60000
	DC.L $00E80000,$00EA0000,$00EC0000,$00EE0000
FLD:	BLK.L 25*4,$0
	DC.L $01004200
	DC.L $F401FFFE,$01000200
	DC.L $FFFFFFFE

COP2:	BLK.B 600,0
COP3:	BLK.B 600,0

FLDPAT:	DC.L $0001FFFE,$01004200,$0001FFFE,$01000200
GRNAM:	DC.B "graphics.library",0
	EVEN

********** CREATE FLD + BITPLANE DATAS IN COPPER LIST **********

FLDFIX:	MOVE.W #$F5,D0
	LEA FLD(PC),A0
	MOVE.W #24,D2
FLDL:	LEA FLDPAT(PC),A1
	MOVE.B D0,(A1)
	MOVE.B D0,8(A1)
	MOVEQ #3,D1
FLDX:	MOVE.L (A1)+,(A0)+
	DBF D1,FLDX	
	DBF D2,FLDL
	
	LEA PLANES+2(PC),A0
	LEA BMAP(PC),A1
	MOVE.L A1,D1
	MOVEQ #3,D0
PLXLP:	SWAP D1
	MOVE.W D1,(A0)
	SWAP D1
	MOVE.W D1,4(A0)
	ADDI.L #8000,D1
	ADDQ.L #8,A0
	DBF D0,PLXLP
	RTS

********** RESTORE SYSTEM COPPER LIST **********

GRBAS:	DC.L 0

GRFET:	MOVE.L 4,A6
	LEA GRNAM(PC),A1
	CLR.L D0
	JSR -30-522(A6)
	LEA GRBAS(PC),A0
	MOVE.L D0,(A0)
	MOVE.L D0,A6

	MOVE.L (A0),A0
	MOVE.L 38(A0),$DFF080
	MOVE.W #0,$DFF088

	MOVE.L 4,A6
	LEA GRBAS(PC),A0
	MOVE.L (A0),A1
	JSR -414(A6)
	RTS

ST2:	BSR FLDFIX
	LEA COPLIST(PC),A0
	MOVE.L A0,$DFF080
	CLR.W $DFF088
	BSR SCRUP

MOUSE:	BTST #6,$BFE001
	BNE MOUSE

	MOVE.L 4,A6
	JSR -138(A6)
	BSR GRFET
	MOVE.W #$8020,$DFF096
	MOVE.W #$8070,$DFF09A
	CLR.L D0
	RTS

********** FLD MODIFY **********

FLDTAB:	BLK.B 26,0
	EVEN

MODIFY:	MOVEM.L D0-D1/A0-A1,-(A7)
VBLANK:	MOVE.L $DFF004,D0
	ANDI.L #$1FF00,D0
	BNE VBLANK

	LEA FLD(PC),A0
	LEA FLDTAB(PC),A1
	MOVE.W #24,D1
	MOVE.W #$2C,D0
MODLOOP:MOVE.B D0,(A0)
	ADDI.B #1,D0
MD2:	MOVE.B D0,8(A0)
	ADD.B (A1)+,D0
MD3:	ADDI.L #16,A0
	DBF D1,MODLOOP

	LEA COPLIST(PC),A0
	LEA COPA(PC),A1
	LEA CURCOP(PC),A2
	MOVE.W (A2),D0
	MOVE.L (A1,D0.W),A1
	MOVE.W #575,D0
COPCOP:	MOVE.B (A0)+,(A1)+
	DBF D0,COPCOP

	LEA COPA(PC),A1
	LEA CURCOP(PC),A2
	MOVE.W (A2),D0
	MOVE.L (A1,D0.W),$DFF080
	CLR.W $DFF088
	EORI.W #4,(A2)
	MOVEM.L (A7)+,D0-D1/A0-A1
	RTS

ALLRAS:	MOVEM.L D0-D1/A0,-(A7)
ALLLOOP:LEA FLDTAB(PC),A0
	MOVE.W #24,D1
ALLLOO2:MOVE.B D0,(A0)+
	DBF D1,ALLLOO2
	BSR MODIFY
	MOVEM.L (A7)+,D0-D1/A0
	RTS

SCRUP:	CLR.W D1
SCRLOOP:LEA BOUNCE(PC),A0
	MOVE.B (A0,D1.W),D0
	BSR ALLRAS
	ADDI.B #1,D1
	CMPI.B #$70,D1
	BNE SCRLOOP
	RTS

BOUNCE:	BLK.B $70,0
BMAP:	BLK.B 32000,0
ENDE:
